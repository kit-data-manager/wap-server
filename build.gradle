plugins {
    id "org.springframework.boot" version "3.5.7"
    id "io.spring.dependency-management" version "1.1.7"
    id "io.freefair.maven-publish-java" version "8.14"
    id "com.github.kt3k.coveralls" version "2.12.2"
    id "org.owasp.dependencycheck" version "12.1.5"
    id 'org.asciidoctor.jvm.convert' version '4.0.4'
    id "net.researchgate.release" version "3.1.0"
    id "com.gorylenko.gradle-git-properties" version "2.5.2"
    id 'nebula.lint' version "20.6.2"
    id 'java'
    id 'jacoco'
}

apply plugin: 'project-report'

gradleLint.rules = ['all-dependency', 'unused-dependency']

//make sure that git properties plugin does not fail on build without access to the git repo information (zip download etc.)
gitProperties {
    failOnNoGitDirectory = false
}

jar {
    archiveBaseName = 'wap-server'
    // version is defined in file 'gradle.properties'
    archiveVersion = System.getenv('version')
}

repositories {
    mavenLocal()
    mavenCentral()
}


configurations {
   all*.exclude module : 'spring-boot-starter-logging'
}

ext {
    set('jenaVersion', "5.4.0")
    set('springDocVersion', "2.5.0")
    //set('keycloakVersion', "19.0.0")

    // directory for generated code snippets during tests
    snippetsDir = file("build/generated-snippets")
}

println "Running gradle version: $gradle.gradleVersion"
println "Building ${name} version: ${version}"
println "JDK version: ${JavaVersion.current()}"

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

if (System.getProperty('profile') == 'minimal') {
    println 'Using minimal profile for building ' + project.getName()
    apply from: 'gradle/profile-minimal.gradle'
} else {
    println 'Using default profile executing all tests for building ' + project.getName()
    apply from: 'gradle/profile-complete.gradle'
}

sourceSets {
    main {
        resources {
            srcDir '.'
            includes = [
                    'doc/**/*.*',
                    'webcontent/**/*.*',
                    'schemas/**/*.*',
                    'profiles/**/FRAME_*.*'
            ]
        }
    }
}

dependencies {
   // boot starter
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-data-rest"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    //implementation "org.springframework.boot:spring-boot-starter-actuator"

    //implementation "org.springframework.security:spring-security-web"
    //implementation "org.springframework.security:spring-security-config"


    // cloud support
    //implementation "org.springframework.cloud:spring-cloud-starter-config"
    //implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
   // implementation "org.springframework.cloud:spring-cloud-gateway-mvc"
    //implementation 'de.codecentric:spring-boot-admin-starter-client'

    // springdoc
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springDocVersion}"

    //Keycloak
    // implementation "org.keycloak:keycloak-spring-boot-starter:${keycloakVersion}"
    //implementation 'com.nimbusds:nimbus-jose-jwt:10.3'
    //implementation "io.jsonwebtoken:jjwt-api:0.11.5"
    //implementation "io.jsonwebtoken:jjwt-impl:0.11.5"
    //implementation "io.jsonwebtoken:jjwt-jackson:0.11.5"

    //Jena
    implementation "org.apache.jena:apache-jena-libs:${jenaVersion}"
    implementation "org.apache.jena:jena-fuseki:${jenaVersion}"
    implementation "org.apache.jena:jena-fuseki-server:${jenaVersion}"
    implementation "org.apache.jena:jena-commonsrdf:${jenaVersion}"

    implementation "org.apache.commons:commons-rdf-jsonld-java:0.5.0"
    implementation "com.github.java-json-tools:json-schema-validator:2.2.14"

    implementation "org.apache.commons:commons-collections4:4.5.0"

     // driver for postgres
    runtimeOnly 'org.postgresql:postgresql:42.7.2'
    //driver for h2
    runtimeOnly 'com.h2database:h2:2.3.232'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework:spring-test"
   // testImplementation "org.springframework.security:spring-security-test"

    testImplementation "io.rest-assured:rest-assured"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "io.specto:hoverfly-java-junit5:0.20.2"

    //Java 11 Support
    testImplementation "org.mockito:mockito-inline:5.2.0"
    //testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation "com.sun.xml.bind:jaxb-core:2.3.0.1"
    testImplementation "com.sun.xml.bind:jaxb-impl:2.3.0.1"
    testImplementation "javax.activation:activation:1.1.1"
    testImplementation "org.javassist:javassist:3.23.1-GA"
    testImplementation "javax.xml.bind:jaxb-api:2.3.1"
}

if (project.hasProperty('release')) {
    println 'Using \'release\' profile for building ' + project.getName()
    apply from: 'gradle/profile-deploy.gradle'
}

test {
    useJUnitPlatform()
 if (!JavaVersion.current().isJava8()) {
        println 'Allow access to private methods...'
        jvmArgs '--illegal-access=permit'
    }
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16)) {
        println 'Allow access to private methods of package java.util.stream...'
        jvmArgs '--add-opens=java.base/java.util.stream=ALL-UNNAMED'
   }
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
    }

   environment "spring.config.location", "classpath:/test-config/"
}

tasks.withType(Test).configureEach {
    testLogging {
        events 'started', 'passed'
    }
}

springBoot {
    buildInfo()
}

bootJar {
    println 'Create bootable jar...'
    archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.springframework.boot.loader.launch.PropertiesLauncher'
    }
    launchScript()
}

// task for printing project name.
tasks.register('printProjectName') {
    doLast {
        println "${project.name}"
    }
}
